buildscript {
	dependencies {
		//classpath fileTree(dir: "libs", include: "fabric-loom-0.7.local.jar")
	}
}

import net.fabricmc.loom.task.RemapJarTask;
plugins {
	id 'maven-publish'
	id 'fabric-loom' version "1.4-SNAPSHOT"
}

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven {
		name = 'legacy-fabric'
		url = 'https://maven.legacyfabric.net'
	}
	// Fixed LWJGL on Linux
	maven {
		name = 'Babric'
		url = 'https://maven.glass-launcher.net/babric'
	}
	maven {
		url file('mavenRepo')
	}
	mavenCentral() //For fastutil
}

loom {
	setIntermediaryUrl('https://maven.legacyfabric.net/net/legacyfabric/intermediary/%1$s/intermediary-%1$s-v2.jar');
}

def lwjglVersion = System.getProperty("os.name").toLowerCase().contains("mac") ? "2.9.1" : "2.9.0"

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "btw.community:mappings:1.0.3"
	//mappings fileTree(dir: "custom_mappings", include: "**.zip")
	//mappings "net.legacyfabric:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	implementation 'net.fabricmc:fabric-loom:1.4-SNAPSHOT'

	implementation 'org.apache.logging.log4j:log4j-core:2.19.0'
	implementation 'org.apache.logging.log4j:log4j-api:2.19.0'

	implementation "org.lwjgl.lwjgl:lwjgl_util:${lwjglVersion}"
	implementation "org.lwjgl.lwjgl:lwjgl:${lwjglVersion}"
	implementation "org.lwjgl.lwjgl:lwjgl-platform:${lwjglVersion}"

	implementation fileTree(dir: "libs", include: "**.zip")
	implementation fileTree(dir: "BTW_dev", include: "**.zip")

	implementation 'com.google.guava:guava:14.0.1'

	// This is what MC 1.6 uses
	implementation 'com.google.code.gson:gson:2.2.2'

	implementation group: 'it.unimi.dsi', name: 'fastutil', version: '8.5.12'

	implementation 'org.apache.commons:commons-lang3:3.13.0'

	//runtimeOnly fileTree(dir: "build/devlibs", include: "${archives_base_name}-${mod_version}-dev.jar")

}

configurations {
	compileClasspath.exclude(group: 'net.minecraft', module: 'minecraft-merged')
}

configurations.all {
	resolutionStrategy {
		dependencySubstitution {
			substitute module('org.lwjgl.lwjgl:lwjgl_util:2.9.1-nightly-20130708-debug3') with {
				module("org.lwjgl.lwjgl:lwjgl_util:${lwjglVersion}")
			}
			substitute module('org.lwjgl.lwjgl:lwjgl:2.9.1-nightly-20130708-debug3') with {
				module("org.lwjgl.lwjgl:lwjgl:${lwjglVersion}")
			}
		}
		force "org.lwjgl.lwjgl:lwjgl-platform:${lwjglVersion}"
	}
}

processResources {
	inputs.property "version", project.version
	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
}

java {
	withSourcesJar()
}

jar {
	from sourceSets.main.output.resourcesDir
	from sourceSets.main.output.classesDirs
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

loom {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

runClient {
	dependsOn(jar)
}

runServer {
	dependsOn(jar)
}